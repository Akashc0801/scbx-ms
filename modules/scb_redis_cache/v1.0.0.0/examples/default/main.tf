terraform {
  required_version = "~> 1.9"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 4.0"
    }
  }
}

provider "azurerm" {
  features {
    resource_group {
      prevent_deletion_if_contains_resources = false
    }
  }
}

locals {
  tags = {
    scenario = "default"
  }
}

# This is required for resource modules
resource "azurerm_resource_group" "this" {
  location = "eastus"
  name     = "rg-redis-default-example"
}

# create a virtual network
resource "azurerm_virtual_network" "this" {
  location            = azurerm_resource_group.this.location
  name                = "endppoint-vnet"
  resource_group_name = azurerm_resource_group.this.name
  address_space       = ["10.0.0.0/16"]
}

# create a subnet for the private endpoint
resource "azurerm_subnet" "endpoint" {
  #checkov:skip=CKV2_AZURE_31:Example code - NSG association not in scope
  address_prefixes     = ["10.0.2.0/24"]
  name                 = "endpoint"
  resource_group_name  = azurerm_resource_group.this.name
  virtual_network_name = azurerm_virtual_network.this.name
}

resource "azurerm_private_dns_zone" "this" {
  name                = "privatelink.redis.cache.windows.net"
  resource_group_name = azurerm_resource_group.this.name
}

resource "azurerm_private_dns_zone_virtual_network_link" "this" {
  name                  = "vnet-link"
  private_dns_zone_name = azurerm_private_dns_zone.this.name
  resource_group_name   = azurerm_resource_group.this.name
  virtual_network_id    = azurerm_virtual_network.this.id
}

resource "azurerm_log_analytics_workspace" "this_workspace" {
  location            = azurerm_resource_group.this.location
  name                = "law-redis-default-example"
  resource_group_name = azurerm_resource_group.this.name
  retention_in_days   = 30
  sku                 = "PerGB2018"
  tags                = local.tags
}

# This is the module call
module "default" {
  source = "../../"

  location            = azurerm_resource_group.this.location
  name                = "redis-default-example"
  resource_group_name = azurerm_resource_group.this.name
  diagnostic_settings = {
    diag_setting_1 = {
      name                           = "diagSetting1"
      log_groups                     = ["allLogs"]
      metric_categories              = ["AllMetrics"]
      log_analytics_destination_type = null
      workspace_resource_id          = azurerm_log_analytics_workspace.this_workspace.id
    }
  }
  enable_telemetry = var.enable_telemetry
  managed_identities = {
    system_assigned = true
  }
  private_endpoints = {
    endpoint1 = {
      subnet_resource_id            = azurerm_subnet.endpoint.id
      private_dns_zone_group_name   = "private-dns-zone-group"
      private_dns_zone_resource_ids = [azurerm_private_dns_zone.this.id]
    }
  }
  public_network_access_enabled = false
  redis_configuration = {
    maxmemory_reserved = 1330
    maxmemory_delta    = 1330
    maxmemory_policy   = "allkeys-lru"
  }
  tags = local.tags
}
